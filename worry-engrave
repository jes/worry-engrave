#!/usr/bin/perl

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/lib";

use WorryEngrave::CNC;
use WorryEngrave::Text;

use Getopt::Long;
use Term::Screen;

my $dryrun;

GetOptions(
    'dry-run' => \$dryrun,
    'help' => sub { usage(0) },
    'usage' => sub { usage(0) },
) or usage(1);

connect_to_router();

my $scr = Term::Screen->new or die "can't get Term::Screen\n";

my ($curx,$cury,$curz) = (0,0,0);
my $step = 10; # mm
my $lastch = 0;
my $text = 'HOPE';

my $safez = 20; # mm
my $safex = 0; # mm
my $safey = 200; # mm
my $fastfeed = 1000; # mm/min
my $fastprobe = 200; # mm/min
my $slowprobe = 50; # mm/min
my $cutspeed = 30; # mm/min
my $cutheight = -0.25; # mm
my $probeheight = 24.83 - 11.8 - 1.0; # mm # probe is 24.83mm tall, jig platform is 11.8mm above table, disc is 1mm thick

($curx,$cury,$curz) = WorryEngrave::CNC->get_coords() unless $dryrun;

while (1) {
    draw();
    my $ch = $scr->getch;
    $lastch = $ch;
    if ($ch eq '0') { # zero one of the axes
        draw();
        $ch = $scr->getch;
        $curx = 0 if $ch eq 'x';
        $cury = 0 if $ch eq 'y';
        $curz = 0 if $ch eq 'z';
        WorryEngrave::CNC->send("G92 X$curx Y$cury Z$curz");
    }
    if ($ch eq 'q') { # quit
        $scr->clrscr;
        exit(0);
    }
    if ($ch eq '+') { # increase step size
        $step = $step*10;
    }
    if ($ch eq '-') { # decrease step size
        $step = $step/10;
    }
    if ($ch =~ /^k[lrud]$/) { # arrow key: move x or y
        $curx -= $step if $ch =~ /l/;
        $curx += $step if $ch =~ /r/;
        $cury -= $step if $ch =~ /d/;
        $cury += $step if $ch =~ /u/;
        WorryEngrave::CNC->send("G0 X$curx Y$cury Z$curz");
    }
    if ($ch =~ /^pg(up|dn)$/) { # move z
        $curz -= $step if $ch =~ /dn/;
        $curz += $step if $ch =~ /up/;
        WorryEngrave::CNC->send("G0 X$curx Y$cury Z$curz");
    }
    if ($ch eq 'e') { # engrave text
        ready();
        engrave($text);
        unready();
    }
    if ($ch eq 'c') { # change text
        $text = input(8, $text, "New text");
    }
    if ($ch eq 'h') { # help
        help();
    }
    if ($ch eq 'g') { # get coords
        ($curx,$cury,$curz) = WorryEngrave::CNC->get_coords() unless $dryrun;
    }
    if ($ch eq 'a') {
        $cutheight = sprintf("%.3f", input(10, $cutheight, "New cut height"));
    }
    if ($ch eq 'p') { # probe
        probez();
        ($curx,$cury,$curz) = WorryEngrave::CNC->get_coords() unless $dryrun;
    }
}

sub input {
    my ($line, $cur, $prompt) = @_;

    my $new = '';

    while (1) {
        $scr->at($line, 0);
        $scr->clreol;
        $scr->at($line, 2);
        $scr->puts("$prompt: $new");

        my $ch = $scr->getch;
        last if $ch eq "\r"; # enter
        return $cur if ord($ch) == 7; # escape
        if (ord($ch) == 127) { # backspace
            if (length($new) > 0) {
                substr($new, length($new)-1, 1, '');
            }
        } else {
            $new .= $ch;
        }
    }

    return $new;
}

sub draw {
    $scr->clrscr;
    $scr->at(0, 2);
    $scr->puts("--- WORRY ENGRAVE by jes ---");

    $scr->at(2, 2);
    $scr->puts("X:");
    $scr->bold->puts(sprintf("%7.2f",$curx))->normal;
    $scr->puts(" [0 X] [left] [right]");

    $scr->at(3, 2);
    $scr->puts("Y:");
    $scr->bold->puts(sprintf("%7.2f",$cury))->normal;
    $scr->puts(" [0 Y] [up] [down]");

    $scr->at(4, 2);
    $scr->puts("Z:");
    $scr->bold->puts(sprintf("%7.2f",$curz))->normal;
    $scr->puts(" [0 Z] [PgUp] [PgDn] [P]robe");

    $scr->at(6, 2);
    $scr->puts("Step: ");
    $scr->bold->puts(sprintf("%g",$step))->normal;
    $scr->puts(" [+] [-]");

    $scr->at(8, 2);
    $scr->puts("Text: ");
    $scr->bold->puts($text)->normal;
    $scr->puts(" [E]ngrave [C]hange");

    $scr->at(10, 2);
    $scr->puts("Cut height: ");
    $scr->bold->puts($cutheight)->normal;
    $scr->puts(" Ch[a]nge");

    $scr->at(12, 2);
    $scr->puts("[Q]uit [H]elp [G]et coords");
}

sub ready {
    WorryEngrave::CNC->send("M3 S24000");
    WorryEngrave::CNC->send("G1 Z$safez F$fastfeed");
    WorryEngrave::CNC->send("G0 X0 Y0 F$fastfeed");
}

sub check {
    my ($question, $instruction) = @_;

    $scr->at(8, 2);
    $scr->clreol;
    $scr->puts("$question [Y]es [N]o");

    my $gotn = 0;

    while (1) {
        my $ch = $scr->getch;
        last if $ch eq 'y';
        if ($ch eq 'n') {
            if ($gotn || !$instruction) {
                return 0;
            } else {
                $gotn = 1;
                $scr->at(8, 2);
                $scr->clreol;
                $scr->puts("$instruction $question [Y]es [N]o");
            }
        }
    }

    return 1;
}

sub engrave {
    my ($text) = @_;

    return if !check("Is the spindle spinning up?", "Switch to PC mode and press green button.");

    my $gcode = WorryEngrave::Text->mkgcode($text,
        centrex => 0,
        centrey => 0,
        diameter => 17,
        hspeed => $cutspeed,
        cutheight => $cutheight,
    );

    my @lines = split /\n/, $gcode;
    my $n = 0;

    # wait for spindle to spin up
    for (1..10) {
        $scr->at(8, 2);
        $scr->clreol;
        $scr->puts("Engraving $text: 0.0% [hit any key to abort]");
        return if $scr->key_pressed; # abort on any keypress
        sleep(1);
    };

    for my $i (0 .. $#lines) {
        my $l = $lines[$i];
        my $pct = sprintf("%.1f", 100 * $i / @lines);

        $scr->at(8, 2);
        $scr->clreol;
        $scr->puts("Engraving $text: $pct%");

        WorryEngrave::CNC->send($l);
    }
}

sub unready {
    WorryEngrave::CNC->send("M5 S0");
    WorryEngrave::CNC->send("G1 Z$safez F$fastfeed");
    WorryEngrave::CNC->send("G0 X$safex Y$safey F$fastfeed");

    ($curx,$cury,$curz) = WorryEngrave::CNC->get_coords() unless $dryrun;
}

sub help {
    $scr->clrscr;
    $scr->at(0, 2);
    $scr->puts("--- WORRY ENGRAVE by jes ---");

    my @help = (
        "  SETUP",
        "1. Turn the CNC machine on.",
        "2. Use [PgUp]/[PgDn] to position tool above jig.",
        "3. Use arrow keys to jog tool to platform.",
        "4. Use [+]/[-] to adjust step size and jog tool to exact centre using arrow keys.",
        "5. Use [0 X] and [0 Y] to zero the X and Y coordinates.",
        "6. Place probe next to jig, plug it in, clip to tool.",
        "7. Use arrow keys to jog tool to probe.",
        "8. Use [P] to probe Z height.",
        "9. Remove the probe.",
        "",
        "  ENGRAVING",
        "1. Use [C] to set the text.",
        "2. Place a blank pendant on the platform.",
        "3. Making sure it is pushed home, tighten the clamp.",
        "4. Use [E] to engrave the text on the first side.",
        "5. Loosen the clamp, rotate the pendant around the Y axis, reclamp.",
        "6. Use [E] to engrave the text on the second side.",
        "7. Loosen the clamp and remove the pendant.",
        "",
        "[hit any key to go back]",
    );

    my $line = 2;
    for my $l (@help) {
        $scr->at($line++, 2);
        $scr->puts($l);
    }

    $scr->getch;
}

sub probez {
    return if !check("Is the probe in place underneath the cutting tool?", "Put the probe in place.");

    # probe downwards, fast
    WorryEngrave::CNC->send("G38.2 Z-50 F$fastprobe");
    # move up by 1mm
    WorryEngrave::CNC->send("G91 G0 Z1 F$fastfeed");
    WorryEngrave::CNC->send("G90");
    # prove downwards, slowly
    WorryEngrave::CNC->send("G38.2 Z-5 F$slowprobe");
    # set Z coord
    WorryEngrave::CNC->send("G92 Z$probeheight");
    # pick tool up
    WorryEngrave::CNC->send("G1 Z$safez F$fastfeed");

    # don't move the tool unless they've already set X and Y
    return if !check("Have you already zeroed X and Y?");

    # don't move the tool until they remove the probe
    return if !check("Have you removed the probe?", "Remove the probe.");

    # return to safe point
    WorryEngrave::CNC->send("G0 X$safex Y$safey F$fastfeed");
}

# this runs before Term::Screen is set up
sub connect_to_router {
    print "Connecting...\n";

    if ($dryrun) {
        WorryEngrave::CNC->dryrun;
    } else {
        WorryEngrave::CNC->connect("/dev/ttyACM0")
            or WorryEngrave::CNC->connect("/dev/ttyACM1")
                or die "can't connect";
    }

    WorryEngrave::CNC->send("G21 G90 G54 M5");

    print "OK\n";
}

sub usage {
    my ($rc) = @_;

    print "usage: worry-engrave [--dry-run]\n";

    exit($rc);
}
